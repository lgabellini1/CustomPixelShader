#include "Custom.ush"

float3 TargetColour;

Texture2D<float4> SceneColourTexture;

float4 MainPS(float4 SvPosition : SV_POSITION) : SV_Target0 
{
	const int MipmapLevel = 0;
	const float4 SceneColour = SceneColourTexture.Load(int3(SvPosition.xy, MipmapLevel));
	
#if USE_UNLIT_SCENE_COLOUR
	FScreenSpaceData ScreenSpaceData = GetScreenSpaceDataUint(SvPosition.xy, false);
	const float4 SceneColourUnlit = float4(ScreenSpaceData.GBuffer.BaseColor, 0);
	const float3 SceneColourLab = RGBToLab(SceneColourUnlit.rgb);
#else 
	const float3 SceneColourLab = RGBToLab(SceneColour.rgb);
#endif

	const float DeltaE = DeltaE2000(TargetColour, SceneColourLab, 1, 1, 1);
	
	// If the scene is within the threshold, return scene colour
	// Delta E Perception
	// <= 1.0	Not perceptible by human eyes.
	// 1 - 2	Perceptible through close observation.
	// 2 - 10	Perceptible at a glance.
	// 11 - 49	Colors are more similar than opposite
	// 100	Colors are exact opposite
	FLATTEN
	if (DeltaE < 45) 
	{
		return SceneColour;
	}
	
	// Otherwise return greyscale version of scene colour
	return 0.21 * SceneColour.r + 0.72 * SceneColour.g + 0.07 * SceneColour.b;
}